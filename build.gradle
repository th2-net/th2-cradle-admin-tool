plugins {
    id "com.exactpro.th2.gradle.base" version "0.0.6"
    id "com.exactpro.th2.gradle.component" version "0.0.6" apply false
}

apply plugin: 'java'

ext {
    commonVersion = '5.11.0-dev'
    cradleVersion = '5.3.0-dev'
}

dependencyCheck {
    suppressionFile = "suppressions.xml"
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'application'

    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
    compileJava.options.debugOptions.debugLevel = 'source,lines,vars' // Include debug information

    group = 'com.exactpro.th2'

    repositories {
        mavenCentral()
        maven {
            name 'Sonatype_snapshots'
            url 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
        }
        maven {
            name 'Sonatype_releases'
            url 'https://s01.oss.sonatype.org/content/repositories/releases/'
        }
        configurations.configureEach {
            resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
            resolutionStrategy.cacheDynamicVersionsFor 0, 'seconds'
        }
    }

    dependencies {
        implementation("com.exactpro.th2:common:$commonVersion") {
            exclude group: 'com.squareup.okhttp3', module: 'okhttp'
        }
        implementation "com.exactpro.th2:cradle-core:$cradleVersion"
        implementation "com.exactpro.th2:cradle-cassandra:$cradleVersion"
        implementation 'org.apache.commons:commons-lang3'

        implementation "org.slf4j:slf4j-api"

        testImplementation 'org.junit.jupiter:junit-jupiter:5.10.2'
        testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

        testImplementation 'org.mockito:mockito-inline:5.2.0'
        testImplementation 'org.mockito:mockito-junit-jupiter:5.10.0'
        testImplementation 'com.exactpro.th2:junit-jupiter-integration:0.0.1'
    }

    test {
        useJUnitPlatform {
            excludeTags('integration')
        }
    }

    tasks.register('integrationTest', Test) {
        group = 'verification'
        useJUnitPlatform {
            includeTags('integration')
        }
        testLogging {
            showStandardStreams = true
        }
    }
}