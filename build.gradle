import com.github.jk1.license.filter.LicenseBundleNormalizer
import com.github.jk1.license.render.JsonReportRenderer

plugins {
    id "org.owasp.dependencycheck" version "8.3.1"
    id 'com.github.jk1.dependency-license-report' version '2.5'
    id "de.undercouch.download" version "5.4.0"
}

apply plugin: 'java'
apply plugin: 'application'


ext {
    commonVersion = '5.2.1-dev'
    cradleVersion = '5.0.3-dev'
}

allprojects {

    apply plugin: 'java'
    apply plugin: 'application'

    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
    compileJava.options.debugOptions.debugLevel = 'source,lines,vars' // Include debug information

    group = 'com.exactpro.th2'

    repositories {
        mavenCentral()
        maven {
            name 'Sonatype_snapshots'
            url 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
        }
        maven {
            name 'Sonatype_releases'
            url 'https://s01.oss.sonatype.org/content/repositories/releases/'
        }
        configurations.configureEach {
            resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
            resolutionStrategy.cacheDynamicVersionsFor 0, 'seconds'
        }
    }

    configurations {
        implementation.exclude group: 'com.google.guava', module: 'guava'
    }

    dependencies {

        implementation "com.exactpro.th2:common:$commonVersion"
        implementation(platform("com.exactpro.th2:bom:4.4.0"))
        implementation "com.exactpro.th2:cradle-core:$cradleVersion"
        implementation "com.exactpro.th2:cradle-cassandra:$cradleVersion"
        implementation 'org.apache.commons:commons-lang3:3.12.0'
        implementation 'org.apache.logging.log4j:log4j-1.2-api:2.14.1'

        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.2'
        testImplementation 'org.junit.jupiter:junit-jupiter-params:5.9.2'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.2'

        testImplementation 'org.mockito:mockito-inline:4.10.0'
        testImplementation 'org.mockito:mockito-junit-jupiter:4.10.0'
    }
}

test {
    useJUnitPlatform()
}

dependencyLocking {
    lockAllConfigurations()
}

dependencyCheck {
    formats = ['SARIF', 'JSON', 'HTML']
    failBuildOnCVSS = 5
    suppressionFile = "suppressions.xml"

    analyzers {
        assemblyEnabled = false
        nugetconfEnabled = false
        nodeEnabled = false
    }
}

licenseReport {
    def licenseNormalizerBundlePath = "$buildDir/license-normalizer-bundle.json"

    if (!file(licenseNormalizerBundlePath).exists()) {
        download.run {
            src 'https://raw.githubusercontent.com/th2-net/.github/main/license-compliance/gradle-license-report/license-normalizer-bundle.json'
            dest "$buildDir/license-normalizer-bundle.json"
            overwrite false
        }
    }

    filters = [
            new LicenseBundleNormalizer(licenseNormalizerBundlePath, false)
    ]
    renderers = [
            new JsonReportRenderer('licenses.json', false),
    ]
    excludeOwnGroup = false
    allowedLicensesFile = new URL("https://raw.githubusercontent.com/th2-net/.github/main/license-compliance/gradle-license-report/allowed-licenses.json")
}
