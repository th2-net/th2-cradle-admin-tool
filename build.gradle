plugins {
    alias(libs.plugins.th2.base)
    alias(libs.plugins.th2.component) apply false
}

dependencyCheck {
    suppressionFile = "suppressions.xml"
}

subprojects {
    apply plugin: 'java'

    th2JavaRelease {
        targetJavaVersion.set(JavaVersion.VERSION_11)
    }

    compileJava.options.debugOptions.debugLevel = 'source,lines,vars' // Include debug information

    group = 'com.exactpro.th2'
    version = release_version

    repositories {
        mavenCentral()
        maven {
            name 'Sonatype_snapshots'
            url 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
        }
        maven {
            name 'Sonatype_releases'
            url 'https://s01.oss.sonatype.org/content/repositories/releases/'
        }
        configurations.configureEach {
            resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
            resolutionStrategy.cacheDynamicVersionsFor 0, 'seconds'
        }
    }

    dependencies {
        implementation(libs.cradle.core)
        implementation(libs.cradle.cassandra)
        implementation "org.slf4j:slf4j-api"

        testImplementation(libs.th2.junit.jupiter.integration)

        testImplementation(libs.mockito.core)
        testImplementation(libs.mockito.junit.jupiter)
        testImplementation(libs.junit.jupiter)

        testRuntimeOnly('org.junit.platform:junit-platform-launcher') {
            because 'This artifact will not be added automatically since gradle 9.0. You can read more information here:' +
                    'https://docs.gradle.org/8.3/userguide/upgrading_version_8.html#test_framework_implementation_dependencies'
        }
    }

    test {
        useJUnitPlatform {
            excludeTags('integration')
        }
    }

    tasks.register('integrationTest', Test) {
        group = 'verification'
        systemProperty 'datastax-java-driver.advanced.connection.init-query-timeout', '5 seconds'
        systemProperty 'datastax-java-driver.basic.request.timeout', '10 seconds'
        useJUnitPlatform {
            includeTags('integration')
        }
        testLogging {
            showStandardStreams = true
        }
    }
}