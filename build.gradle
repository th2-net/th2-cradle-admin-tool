import com.github.jk1.license.filter.LicenseBundleNormalizer
import com.github.jk1.license.render.JsonReportRenderer

plugins {
    id "org.owasp.dependencycheck" version "8.3.1"
    id 'com.github.jk1.dependency-license-report' version '2.5'
    id "de.undercouch.download" version "5.4.0"
}

apply plugin: 'java'
apply plugin: 'application'


ext {
    commonVersion = '5.8.0-dev'
    cradleVersion = '5.2.0-TH2-5165-+'
}

allprojects {

    apply plugin: 'java'
    apply plugin: 'application'

    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
    compileJava.options.debugOptions.debugLevel = 'source,lines,vars' // Include debug information

    group = 'com.exactpro.th2'

    repositories {
        mavenCentral()
        maven {
            name 'Sonatype_snapshots'
            url 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
        }
        maven {
            name 'Sonatype_releases'
            url 'https://s01.oss.sonatype.org/content/repositories/releases/'
        }
        configurations.configureEach {
            resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
            resolutionStrategy.cacheDynamicVersionsFor 0, 'seconds'
        }
    }

    dependencies {
        implementation(platform("com.exactpro.th2:bom:4.5.0"))

        implementation("com.exactpro.th2:common:$commonVersion") {
            exclude group: 'com.squareup.okhttp3', module: 'okhttp'
        }
        implementation "com.exactpro.th2:cradle-core:$cradleVersion"
        implementation "com.exactpro.th2:cradle-cassandra:$cradleVersion"
        implementation 'org.apache.commons:commons-lang3'

        implementation "org.slf4j:slf4j-api"

        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.10.2'
        testImplementation 'org.junit.jupiter:junit-jupiter-params:5.10.2'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.10.2'

        testImplementation 'org.mockito:mockito-inline:5.2.0'
        testImplementation 'org.mockito:mockito-junit-jupiter:5.10.0'
        testImplementation 'com.exactpro.th2:junit-jupiter-integration:0.0.1-master-6956603819-5241ee5-SNAPSHOT'
    }

    test {
        useJUnitPlatform {
            excludeTags('integration')
        }
    }

    tasks.register('integrationTest', Test) {
        group = 'verification'
        useJUnitPlatform {
            includeTags('integration')
        }
        testLogging {
            showStandardStreams = true
        }
    }
}

dependencyLocking {
    lockAllConfigurations()
}

dependencyCheck {
    formats = ['SARIF', 'JSON', 'HTML']
    failBuildOnCVSS = 5
    suppressionFile = "suppressions.xml"

    analyzers {
        assemblyEnabled = false
        nugetconfEnabled = false
        nodeEnabled = false
    }
}

licenseReport {
    def licenseNormalizerBundlePath = "$buildDir/license-normalizer-bundle.json"

    if (!file(licenseNormalizerBundlePath).exists()) {
        download.run {
            src 'https://raw.githubusercontent.com/th2-net/.github/main/license-compliance/gradle-license-report/license-normalizer-bundle.json'
            dest "$buildDir/license-normalizer-bundle.json"
            overwrite false
        }
    }

    filters = [
            new LicenseBundleNormalizer(licenseNormalizerBundlePath, false)
    ]
    renderers = [
            new JsonReportRenderer('licenses.json', false),
    ]
    excludeOwnGroup = false
    allowedLicensesFile = new URL("https://raw.githubusercontent.com/th2-net/.github/main/license-compliance/gradle-license-report/allowed-licenses.json")
}
